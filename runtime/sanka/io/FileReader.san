// -*- mode: Java; -*-
package sanka.io;

class FileReader {
    c__include "stdio.h";
    c__include "errno.h";
    c__field "FILE *fp";

    private String filename;

    int open(String filename) {
        this.filename = filename;
        c__stmt "char rd[2] = {'r', 0}";
        c__stmt "this->fp = fopen(filename, rd)";
        c__stmt "return this->fp != NULL ? 0 : errno";
    }

    /**
     * See Reader.read().
     */
    int read(byte[] buffer) {
        c__stmt "return fread(buffer->data, 1, buffer->length, this->fp)";
    }

    int close() {
        c__stmt "int status = fclose(this->fp)";
        c__stmt "this->fp = NULL";
        c__stmt "return status == 0 ? 0 : errno";
    }

    /**
     * Read the entire file contents into memory and close the stream.
     */
    byte[] readFile() {
        var stats = new FileStats();
        if (new File(this.filename).getStats(stats, true) != 0) {
            return null;
        }
        var body = new byte[](stats.length);
        read(body);
        close();
        return body;
    }
}
