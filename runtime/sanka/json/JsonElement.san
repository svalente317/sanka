// -*- mode: Java; -*-
package sanka.json;

class JsonElement {
    const NULL_TYPE = 0;
    const BOOLEAN_TYPE = 1;
    const INT_TYPE = 2;
    const DOUBLE_TYPE = 3;
    const STRING_TYPE = 4;
    const ARRAY_TYPE = 5;
    const OBJECT_TYPE = 6;

    int type;
    c__field "union { long i; double d; const char *s; struct array *arr; struct JsonObject *obj; } data";

    JsonElement() {
        this.type = NULL_TYPE;
    }

    JsonElement makeBoolean(boolean value) {
        this.type = BOOLEAN_TYPE;
        c__stmt "this->data.i = value";
        return this;
    }

    JsonElement makeShort(short value) {
        this.type = INT_TYPE;
        c__stmt "this->data.i = value";
        return this;
    }

    JsonElement makeInt(int value) {
        this.type = INT_TYPE;
        c__stmt "this->data.i = value";
        return this;
    }

    JsonElement makeLong(long value) {
        this.type = INT_TYPE;
        c__stmt "this->data.i = value";
        return this;
    }

    JsonElement makeFloat(float value) {
        this.type = DOUBLE_TYPE;
        c__stmt "this->data.d = value";
        return this;
    }

    JsonElement makeDouble(double value) {
        this.type = DOUBLE_TYPE;
        c__stmt "this->data.d = value";
        return this;
    }

    JsonElement makeString(String value) {
        if (value == null) {
            this.type = NULL_TYPE;
        } else {
            this.type = STRING_TYPE;
            c__stmt "this->data.s = value";
        }
        return this;
    }

    JsonElement makeArray(JsonElement[] value) {
        this.type = (value == null ? NULL_TYPE : ARRAY_TYPE);
        c__stmt "this->data.arr = value";
        return this;
    }

    JsonElement makeObject(JsonObject value) {
        this.type = (value == null ? NULL_TYPE : OBJECT_TYPE);
        c__stmt "this->data.obj = value";
        return this;
    }

    boolean getAsBoolean() {
        if (this.type != BOOLEAN_TYPE) {
            return false;
        }
        c__stmt "return (int) this->data.i";
    }

    short getAsShort() {
        if (this.type != INT_TYPE) {
            return 0;
        }
        c__stmt "return (short) this->data.i";
    }

    int getAsInt() {
        if (this.type != INT_TYPE) {
            return 0;
        }
        c__stmt "return (int) this->data.i";
    }

    long getAsLong() {
        if (this.type != INT_TYPE) {
            return 0;
        }
        c__stmt "return this->data.i";
    }

    float getAsFloat() {
        if (this.type != DOUBLE_TYPE) {
            return 0;
        }
        c__stmt "return (float) this->data.d";
    }

    double getAsDouble() {
        if (this.type != DOUBLE_TYPE) {
            return 0;
        }
        c__stmt "return this->data.d";
    }

    String getAsString() {
        if (this.type != STRING_TYPE) {
            return null;
        }
        c__stmt "return this->data.s";
    }

    JsonElement[] getAsArray() {
        if (this.type != ARRAY_TYPE) {
            return null;
        }
        c__stmt "return this->data.arr";
    }

    JsonObject getAsObject() {
        if (this.type != OBJECT_TYPE) {
            return null;
        }
        c__stmt "return this->data.obj";
    }

    String toString() {
        switch (this.type) {
        case NULL_TYPE:
            return "null";
        case BOOLEAN_TYPE:
            return getAsBoolean() ? "true" : "false";
        case INT_TYPE:
            return "" + getAsLong();
        case DOUBLE_TYPE:
            return "" + getAsDouble();
        case STRING_TYPE:
            var text = getAsString();
            return StringEncoder.get().encodeAndQuote(text);
        case ARRAY_TYPE:
            var arr = getAsArray();
            if (arr.length == 0) {
                return "[]";
            }
            var s = arr[0].toString();
            for (var idx = 1; idx < arr.length; idx++) {
                s = s + "," + arr[idx].toString();
            }
            return "[" + s + "]";
        case OBJECT_TYPE:
            return getAsObject().toString();
        }
        return null;
    }

    static JsonElement parse(String text) {
        return JsonParser.parse(text);
    }

    JsonElement toJson() {
        return this;
    }

    void fromJson(JsonElement element) {
        this.type = element.type;
        c__stmt "this->data = element->data";
    }
}
